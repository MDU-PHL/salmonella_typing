'''
A Snakefile for Salmonella typing at MDU
'''

import pandas

configfile: 'config_sistr.yaml'
input_file = config['input_file']
stm_control = config['stm_control']

SAMPLES = pandas.read_csv(input_file, sep=None, engine='python', encoding='utf8').set_index("SEQID", drop=False)

rule all:
    input:
        "SISTR_LIMS.xlsx",
        input_file

rule test_controls:
    input:
        stm=stm_control,
    output:
        "sistr_control.csv"
    params:
        prefix="sistr_control"
    threads: 2
    shell:
        """
        sistr -i "{input.stm}" "LT2" -f csv -o {params.prefix} -T $TMPDIR --qc --threads {threads}
        """

rule run_sistr:
    input:
        "sistr_control.csv",
        asm="{sample}/contigs.fa"
        
    output:
        "{sample}/sistr.csv"
    threads: 2
    shell:
        """
        sistr -i {input.asm:q} {wildcards.sample} -f csv -o {wildcards.sample}/sistr -T $TMPDIR --qc --threads {threads}
        """

rule gather_results:
    input:
        res=expand("{sample}/sistr.csv", sample=SAMPLES.index),
        # cont="sistr_control.csv"
    output:
        "sistr_concat.csv"
    run:
        import pandas
        print(input)
        combined_tab = pandas.concat([pandas.read_csv(f, engine='python', sep=None) for f in input])
        combined_tab.to_csv(output[0], index=False)

rule process_results:
    input:
        res = "sistr_concat.csv",
        cont = "sistr_control.csv"
    output:
        "SISTR_LIMS.xlsx"
    params:
        prefix = "SISTR_LIMS"
    shell:
        '''
        stype_cli.py parse -l -p {params.prefix} {input.cont} {input.res}
        '''
